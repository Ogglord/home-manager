# Justfile
#set shell := ["zsh", "-c"]
set dotenv-load := true
set allow-duplicate-variables := true
set unstable := true
#set allow-duplicate-recipes := true
#set ignore-comments := true

# optional import - in case we are on ublue/bazzite/etc
import? "/usr/share/ublue-os/justfile"

is_ublue := path_exists('/usr/share/ublue-os/justfile')
topgrade_cfg := if is_ublue == "true" { "/usr/share/ublue-os/topgrade.toml" } else { "~/.config/topgrade/topgrade.toml" }
topgrade_msg := if is_ublue == "true" { "ublue mode" } else { "normal mode" }
ZSHRC_LAST_LOADED := x'${ZSHRC_LAST_LOADED:-unset}'

# default is to list commands
default:
  @just --list --unsorted

# update system
update:
    #!/usr/bin/env bash
    echo "Running topgrade in {{topgrade_msg}}..."
    [ -f /usr/bin/ublue-update ] && ublue-update --wait
    /usr/bin/topgrade --config "{{topgrade_cfg}}" --keep

# reload zsh config
reload-zsh:    
    echo "Press ctrl+N to reload zsh config"    

#re-build home-manager config and switch to it
switch:
    home-manager switch -b backup
    just reload-zsh

# re-build home-manager config
build:
    home-manager build

clean:
    sudo nix profile wipe-history --profile /nix/var/nix/profiles/system  --older-than 7d
    nix profile wipe-history --profile   --older-than 7d

shell-test:
    @echo "$SHELL"
    @echo "Loaded: {{ZSHRC_LAST_LOADED}}"

default_comment := 'auto commit'

[no-cd, script]
push comment=default_comment:
    git add .
    echo "Committing with message: {{comment}}"
    git commit -m "{{comment}}"
    echo "Pushing to remote"
    git push

[no-cd]
pull:
    git pull --rebase --autostash
